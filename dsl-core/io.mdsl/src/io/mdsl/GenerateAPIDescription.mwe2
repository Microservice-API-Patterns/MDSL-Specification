module io.mdsl.GenerateAPIDescription

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "io.mdsl"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "io.mdsl.APIDescription"
			fileExtensions = "mdsl"
			
			fragment = ui.outline.QuickOutlineFragment2 {}
			// fragment = ui.outline.OutlineTreeProviderFragment2 {}
			fragment = ui.quickfix.QuickfixProviderFragment2 {}
			
			serializer = {
				generateStub = false
			}
			
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				composedCheck = "io.mdsl.validation.DataTypeValidator"
				composedCheck = "io.mdsl.validation.EndpointContractValidator"
				composedCheck = "io.mdsl.validation.MessageExchangePatternValidator"
				composedCheck = "io.mdsl.validation.MAPDecoratorValidator"
				composedCheck = "io.mdsl.validation.BasicBindingValidator"
				composedCheck = "io.mdsl.validation.HTTPBindingValidator"
				composedCheck = "io.mdsl.validation.OneWayChannelValidator"
				composedCheck = "io.mdsl.validation.ChannelContractValidator"
				composedCheck = "io.mdsl.validation.MessageEndpointValidator"
				composedCheck = "io.mdsl.validation.RuntimeExpressionValidator"
			}
			
			junitSupport = {
				junitVersion = "5"
				generateStub = false
			}
			
			formatter = {
				generateStub = true
			}
			
			generator = {
				generateXtendStub = false
			}
		}
	}
}
