buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:4.0.0'
	}
}



def pomXml = new XmlSlurper().parse('pom.xml')

subprojects {
		ext.xtextVersion = '2.35.0'

		apply plugin: 'java-library'
		apply plugin: 'maven-publish'
		apply plugin: 'org.xtext.xtend'
		apply plugin: 'eclipse'
		apply plugin: 'jacoco'
		apply plugin: 'signing'

		dependencies {
			implementation platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
		}

		group = 'io.mdsl'
		version = pomXml.version

		java {
			sourceCompatibility = JavaVersion.VERSION_17
			targetCompatibility = JavaVersion.VERSION_17
		}

		configurations.all {
				exclude group: 'asm'
		}

		repositories {
			mavenCentral()
			maven {
				url "https://plugins.gradle.org/m2/"
			}
		}

	// causes my local build to fail:
	signing {
			sign configurations.archives
	}

	publishing {
		repositories {
			maven {
				def releasesRepoUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
				def snapshotsRepoUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
				url = "${project.version}".endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				credentials {
					username = project.findProperty('ossrhTokenUsername') ?: ""
					password = project.findProperty('ossrhTokenPassword') ?: ""
				}
			}
		}
	}


// workaround for XText+Gradle build problems: https://github.com/eclipse/xtext/issues/1976#issuecomment-862141814
	configurations.all {
		resolutionStrategy {
			eachDependency { DependencyResolveDetails details ->
				if (details.requested.group == 'org.eclipse.platform' && details.requested.name == 'org.eclipse.core.runtime') {
					details.useVersion "3.19.0"
				}
				if (details.requested.group == 'org.eclipse.platform' && details.requested.name == 'org.eclipse.equinox.common') {
					details.useVersion("3.13.0")
				}
			}
		}
	}
}


configure(subprojects.findAll {it.name != 'mdsl-cli'}) {
		apply from: "${rootDir}/gradle/source-layout.gradle"
}
